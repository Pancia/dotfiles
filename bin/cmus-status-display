#!/usr/bin/env fish
#
# NOTE sourced from /opt/homebrew/Cellar/cmus/2.9.1_1/share/doc/cmus/examples
#
# cmus-status-display
#
# Usage:
#   in cmus command ":set status_display_program=cmus-status-display"
#
# This scripts is executed by cmus when status changes:
#   cmus-status-display key1 val1 key2 val2 ...
#
# All keys contain only chars a-z. Values are UTF-8 strings.
#
# Keys: status file url artist album discnumber tracknumber title date
#   - status (stopped, playing, paused) is always given
#   - file or url is given only if track is 'loaded' in cmus
#   - other keys/values are given only if they are available
#

function output
    # Convert to ASCII and escape for Lua double-quoted string: \ → \\, " → \"
    set -l escaped (printf '%s' $argv | iconv -s -c -t ascii | string replace -a '\\' '\\\\' | string replace -a '"' '\\"')
    set -l command "local port = hs.ipc.remotePort(\"cmus\"); port:sendMessage(\"$escaped\", 123); port:delete()"
    hs -c "$command"
end

# Parse key-value pairs into _key variables
set -l i 1
while test $i -le (count $argv)
    set -l key $argv[$i]
    set -l val $argv[(math $i + 1)]
    set -l varname _$key
    set $varname $val
    set i (math $i + 2)
end

set -l message ""
if test -n "$_file"
    set message (printf "%.20s - %.20s" "$_artist" "$_title")
else if test -n "$_url"
    set message (printf "%.20s - %.20s" "$_url" "$_title")
end
# Include status in message so Hammerspoon doesn't need to re-query (avoids race condition)
# Use :: as delimiter (pipe causes shell escaping issues)
output "$_status::$message"
